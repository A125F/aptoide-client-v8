/*
 * Copyright (c) 2016.
 * Modified by SithEngineer on 27/06/2016.
 */

apply plugin: 'com.android.application'
apply plugin: 'android-command'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
    jvmArgs '-noverify'
}

android {
    compileSdkVersion 23
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        targetSdkVersion 23
        versionCode 1
        versionName "8.0.0"
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        // Enable the experimental Jack build tools.
        jackOptions {
            enabled false
        }
    }

    signingConfigs { release }

    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            applicationId "cm.aptoide.pt.aptoideclientv8"
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            versionCode 60
            versionName "8.0.0.27062016"
            minSdkVersion 21
            resValue "string", "authenticatorAccountType", applicationId
            // Enabling multidex support.
//            multiDexEnabled true
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 15
            applicationId "cm.aptoide.pt"
            resValue "string", "authenticatorAccountType", applicationId
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources true
            multiDexEnabled true
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    project.ext { appName = 'aptoide' }
                    def newName = output.outputFile.name
                    newName = newName.replace("vanilla-", "$project.ext.appName-") //"MyAppName" -> I set my app variables in the root project
                    newName = newName.replace("prod-release", versionName)
                    newName = newName.replace("release", versionName)
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }

        debug {
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst 'META-INF/services/javax.annotation.processing.Processor'
        pickFirst 'META-INF/DEPENDENCIES.txt'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/MANIFEST.MF'
        pickFirst 'META-INF/LGPL2.1'
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.1'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"

    debugCompile project(path: ':v8engine', configuration: 'debug')
    releaseCompile project(path: ':v8engine', configuration: 'release')
}

def File propFile = new File('signing.properties')
if (propFile.canRead()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        println 'all good to go'
    } else {
        android.buildTypes.release.signingConfig = null
        println 'signing.properties found but some entries are missing'
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
































